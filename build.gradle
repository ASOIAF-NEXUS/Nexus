import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'application'
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'asoiaf-nexus'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

jar {
    enabled = false
}

java {
    sourceCompatibility = JavaVersion.VERSION_23
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

application {
    mainClass = "asoiafnexus.Main"
}

allprojects {
    apply plugin: "java"

    repositories {
        mavenCentral()
    }

    compileJava {
        options.compilerArgs += [
                "-parameters",
        ]
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.compilerArgs += [
                "-parameters",
        ]
        options.encoding = 'UTF-8'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        testAnnotationProcessor {
            extendsFrom annotationProcessor
        }
        testCompileOnly {
            extendsFrom testAnnotationProcessor
        }
    }

    dependencies {
        implementation(enforcedPlatform(SpringBootPlugin.BOM_COORDINATES))
        annotationProcessor(platform(SpringBootPlugin.BOM_COORDINATES))
        annotationProcessor("org.projectlombok:lombok")

        // Testing
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation(project(":user"))
    implementation(project(":listbuilder"))
    implementation(project(":tournament"))

    testImplementation 'com.squareup.okhttp3:okhttp:4.12.0'

    testImplementation 'io.cucumber:cucumber-java:6.10.4'
    testImplementation 'io.cucumber:cucumber-spring:6.10.4'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:6.10.4'
}

tasks.register('cucumberCli', JavaExec) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(23)
    }
    dependsOn assemble, testClasses
    mainClass = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = [
            '--plugin', 'pretty',
            '--plugin', 'html:target/cucumber-report.html',
            '--glue', 'asoiafnexus.cucumber',
            'src/test/resources']
}

build {
    dependsOn cucumberCli
}